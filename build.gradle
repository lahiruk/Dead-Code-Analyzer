buildscript {
	ext {
		springBootVersion = '1.5.2.RELEASE'
	}
	repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.17')
        classpath('se.transmode.gradle:gradle-docker:1.2')
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'org.springframework.boot'
apply plugin: 'docker'

group = 'lkdevfactory'
project.version = '1.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    jcenter()
	mavenCentral()
}

springBoot  {
    buildInfo()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task copySciTools(type: Copy) {
     from "$tempPath/scitools"
     into "$rootProject.buildDir/docker/scitools"
}

allprojects {
	task copyProjLib(type: Copy) {
	    from "$projectDir/build/libs/$project.name-$rootProject.version"+'.jar'
	    into "$rootProject.buildDir/docker"
	}
}

task manualDockerPush(type: Copy, dependsOn: [":dead-code-worker:bootRepackage", bootRepackage, copySciTools,copyProjLib, ":dead-code-worker:copyProjLib"]) {
	from 'src/main/docker/Ubuntu/Dockerfile'
	into "$buildDir/docker"
}

docker {
    maintainer = 'lkdevfactory <lahiru.karunatilake@aurea.com>'
}

task buildDocker(type: Docker, dependsOn: [":dead-code-worker:bootRepackage", bootRepackage]) {
 //push = true
  applicationName = jar.baseName
  dockerfile = file('src/main/docker/Ubuntu/Dockerfile')
  doFirst {
    copy {
      from jar
      into stageDir
    }
    copy {
      from "dead-code-worker/build/libs/"
      into stageDir
    }
    copy { //For ubuntu docker
      from "$tempPath/scitools"
      into "$stageDir/scitools"
    }
  }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dependencies {
	compile('io.swagger:swagger-inflector:1.0.13-SNAPSHOT')
    compile('com.github.javafaker:javafaker:0.11') {
        exclude group: 'com.github.bmoliveira'
    }
    compile('com.fasterxml.jackson.core:jackson-annotations')
    compile('com.fasterxml.jackson.core:jackson-databind')
    compile('com.fasterxml.jackson.datatype:jackson-datatype-jdk8')
    compile('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-starter-jersey')
    compile('org.springframework.boot:spring-boot-starter-web')
    
    testCompile('io.swagger:swagger-annotations:1.5.8')
    testCompile('com.squareup.okhttp:okhttp:2.7.5')
    testCompile('com.squareup.okhttp:logging-interceptor:2.7.5')
    testCompile('com.google.code.gson:gson:2.6.2')
    testCompile('joda-time:joda-time:2.9.3')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	
	compile files("$tempPath/scitools/bin/linux64/Java/com.scitools.understand.plugin_1.1.3.jar")
	compile('org.zeroturnaround:zt-exec:1.9')
}
